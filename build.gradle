import org.apache.tools.ant.filters.ReplaceTokens
apply plugin: 'java-library'
apply plugin: 'eclipse'

jar.archiveName = project.name + '.jar'

configurations {
	shade
	compile.extendsFrom shade
}

repositories {
	mavenCentral()
	jcenter()
	
	//Spigot
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}
	
	//BungeeTabListPlus
	maven {
		url 'http://nexus.codecrafter47.de/content/repositories/public/'
	}
	
	//Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}
	
	//Redis-Bungee
	maven {
		url "http://repo.md-5.net/content/repositories/snapshots/"
	}
	
	//Jitpack
	maven {
		url 'https://jitpack.io'
	}
	
}

dependencies {
	
	//BungeeTabListPlusAPI
	implementation (group: 'codecrafter47.bungeetablistplus', name: 'bungeetablistplus-api-bungee', version: '2.8.4')
	
	//Redis-Bungee
	implementation (group: 'com.imaginarycode.minecraft', name: 'RedisBungee', version: '0.3.8-SNAPSHOT')
	
	//Nullable annotation
	implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '1.1.0')
	
	//Spigot/Bukkit
	implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.12.2-R0.1-SNAPSHOT')
	
	//Bungeecord
	implementation (group: 'net.md-5', name: 'bungeecord-protocol', version: '1.13-SNAPSHOT')
	implementation (group: 'net.md-5', name: 'bungeecord-proxy', version: '1.13-SNAPSHOT')
	implementation (group: 'net.md-5', name: 'bungeecord-api', version: '1.13-SNAPSHOT')
	
	//Skript
	implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.3.6') {
		transitive = false
	}
	
	//H2 Database
	//shade (group: 'com.h2database', name: 'h2', version: '1.4.197')

}

processResources {
	filter ReplaceTokens, tokens: ["version": project.property("version")]
	from (sourceSets.main.resources.srcDirs) {
		include '*.yml'
	}
	from ("Bungeecord/") {
		include '*.yml', '*.lang'
		into 'Bungeecord/'
	}
	from ("lang/") {
		include '*.lang'
		into 'lang/'
	}
}

jar {
	configurations.shade.each { dep ->
		from(project.zipTree(dep)) {
			exclude 'META-INF', 'META-INF/**'
		}
	}
}